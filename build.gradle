buildscript {
	ext {
		propDepsVersion = '0.0.9.RELEASE'
	}
	repositories {
		mavenCentral()

		maven {
			url 'https://plugins.gradle.org/m2/'
		}
		maven {
			url 'http://repo.spring.io/plugins-release'
		}
	}
	dependencies {
		classpath ("io.spring.gradle:propdeps-plugin:${propDepsVersion}")
	}
}

plugins {
    id 'org.springframework.boot' version '1.5.6.RELEASE'
	id "org.jetbrains.kotlin.jvm" version "1.1.3"
	id "org.jetbrains.kotlin.plugin.spring" version "1.1.3"
}

configure(allprojects) {
	apply plugin: 'java'
	apply plugin: 'kotlin'
	apply plugin: "kotlin-spring"

	apply plugin: 'propdeps'
	apply plugin: 'propdeps-maven'
	apply plugin: 'propdeps-idea'
	apply plugin: 'propdeps-eclipse'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	repositories {
		mavenCentral()
	}

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
	}
}

def frameworkAgnostic = [':application:core', ':application:adapter']
configure(subprojects - frameworkAgnostic.collect { project(it) }) {
	apply plugin: 'io.spring.dependency-management'
}

configure([project(':application')]) {
	apply plugin: 'org.springframework.boot'
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.1'
}

ext {
	versions = [
		commonsLang: '3.6',
        commonsValidator: '1.6'
	]
	test_versions = [
		kotlinTest: '2.0.5'
	]
}

ext.app_tests = [
		spring_test: [
			"org.springframework.boot:spring-boot-starter-test",
		],
		unit_test: [
			"io.kotlintest:kotlintest:${test_versions.kotlinTest}",
			"org.jetbrains.kotlin:kotlin-test",
			"org.jetbrains.kotlin:kotlin-test-junit"
		]
]

apply from: 'gradle/application.gradle'