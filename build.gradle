buildscript {
	ext {
        springBootVersion = '1.5.6.RELEASE'
		propDepsVersion = '0.0.9.RELEASE'
        kotlinVersion = '1.1.3-2'
	}
	repositories {
		mavenCentral()

		maven {
			url 'https://plugins.gradle.org/m2/'
		}
		maven {
			url 'http://repo.spring.io/plugins-release'
		}
	}
	dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath ("io.spring.gradle:propdeps-plugin:${propDepsVersion}")
        classpath ("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath ("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath ("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
	}
}

plugins {
    id "io.spring.dependency-management" version "1.0.3.RELEASE"
}

configure(allprojects) {
	apply plugin: 'java'
	apply plugin: 'kotlin'
	apply plugin: "kotlin-spring"
    apply plugin: "kotlin-jpa"
	apply plugin: "kotlin-kapt"

	apply plugin: 'idea'

	apply plugin: 'propdeps'
	apply plugin: 'propdeps-maven'
	apply plugin: 'propdeps-idea'
	apply plugin: 'propdeps-eclipse'

	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8

	repositories {
		mavenCentral()
	}

	dependencies {
		compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
	}
}

def frameworkAgnostic = [':application:core', ':application:adapter']
configure(subprojects - frameworkAgnostic.collect { project(it) }) {
	apply plugin: 'io.spring.dependency-management'

	dependencyManagement {
		imports {
			mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
		}
	}
}

configure([project(':application')]) {
	apply plugin: 'org.springframework.boot'
}

task wrapper(type: Wrapper) {
	gradleVersion = '4.1'
}

sourceSets {
	main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
	main.kotlin.srcDirs += [file("$buildDir/generated/source/kapt/main")]
}

ext {
	versions = [
		commonsLang: '3.6',
        commonsValidator: '1.6',
		hazelcast: '3.8.4',
		jacksonKotlin: '2.9.0',
		liquibase: '3.5.3',
		modelMapper: '1.1.0',
		querydsl: "4.1.4"
	]
	cache_versions = [
		hazelcast: '3.8.4',
		jcache: '1.0.0'
	]
	ui_versions = [
		googleMaps: '0.2.1',
		thymeleafSpringSecurity: '3.0.2.RELEASE',
		webjarsBootstrap: '3.3.7-1',
		webjarsFancyBox: '2.1.5',
		webjarsFontAwesome: '4.7.0',
		webjarsJquery: '3.2.1',
		webjarsLocator: '0.32-1',
		webjarsModernizr: '2.8.3'
	]
	test_versions = [
		kotlinTest: '2.0.5'
	]
}

ext.app_tests = [
	spring_test: [
		"org.springframework.boot:spring-boot-starter-test",
	],
	unit_test: [
		"io.kotlintest:kotlintest:${test_versions.kotlinTest}",
		"org.jetbrains.kotlin:kotlin-test",
		"org.jetbrains.kotlin:kotlin-test-junit"
	]
]

apply from: 'gradle/application.gradle'